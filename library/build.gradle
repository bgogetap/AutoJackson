plugins {
    id "com.jfrog.bintray" version "1.6"
}
apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

group = PROJECT_GROUP_ID
version = PROJECT_VERSION

dependencies {
    compile "com.google.auto.value:auto-value:$autoValueVersion"
    compile "com.google.auto.service:auto-service:1.0-rc2"
    compile 'com.squareup:javapoet:1.7.0'
    compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    testCompile 'com.google.testing.compile:compile-testing:0.6'
    testCompile 'com.google.truth:truth:0.27'
    testCompile 'junit:junit:4.11'
    testCompile files(org.gradle.internal.jvm.Jvm.current().getToolsJar())
}

bintray {
    Properties properties = new Properties()
    if (file('local.properties').exists()) {
        properties.load(new FileInputStream(file('local.properties')))
    }
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['AutoModulePublication']
    pkg {
        repo = 'maven'
        name = 'auto-jackson'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = PROJECT_VCS_URL
        version {
            name = PROJECT_VERSION
            desc = PROJECT_DESCRIPTION
        }
    }
}

publishing {
    publications {
        AutoModulePublication(MavenPublication) {
            from components.java
            groupId PROJECT_GROUP_ID
            artifactId 'auto-jackson'
            version PROJECT_VERSION

            artifact sourcesJar
        }
    }
}
